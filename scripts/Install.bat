@echo off
cd /d %~dp0
set databaseInstanceORADATA=%SystemDrive%\oracle\oradata
set databaseInstanceName=ORA193
set databaseInstanceName=%databaseInstanceName:~0,8%
set databaseInstanceSYSDBAPassword=password
set databaseListenerName=LISTENER
set databaseListenerPort=1521
echo Setting Oracle Inventory location...
reg add "HKEY_LOCAL_MACHINE\SOFTWARE\Oracle" /v "inst_loc" /t REG_SZ /d "%ProgramFiles%\Oracle\Inventory" /f
echo.
echo Installing Oracle Database software... This may take a while, do not close this window!
"%SystemDrive%\oracle\product\19.0.0\dbhome_1\setup.exe" -responseFile "%~dp0\Install.rsp" ORACLE_BASE="%SystemDrive%\oracle" ORACLE_HOME="%SystemDrive%\oracle\product\19.0.0\dbhome_1" -silent -waitforcompletion -nowait
echo.
echo Creating directories...
md "%SystemDrive%\oracle\audit"
md "%SystemDrive%\oracle\admin\%databaseInstanceName%\adump"
md "%SystemDrive%\oracle\admin\%databaseInstanceName%\dpdump"
md "%SystemDrive%\oracle\admin\%databaseInstanceName%\pfile"
md "%SystemDrive%\oracle\cfgtoollogs\dbca\%databaseInstanceName%"
md "%databaseInstanceORADATA%\%databaseInstanceName%"
echo.
echo Setting Oracle Base directory permissions...
icacls "%SystemDrive%\oracle" /reset /T /C /Q
icacls "%SystemDrive%\oracle" /grant:r ORA_OraDB19Home1_SVCACCTS:(OI)(CI)M /T /C /Q
if not "%databaseInstanceORADATA%"=="%SystemDrive%\oracle\oradata" (
	icacls "%databaseInstanceORADATA%" /reset /T /C /Q
	icacls "%databaseInstanceORADATA%" /grant:r ORA_OraDB19Home1_SVCACCTS:^(OI^)^(CI^)M /T /C /Q
)
echo Installing database instance... This may take a while, do not close this window!
set ORACLE_SID=%databaseInstanceName%
set ORACLE_HOME=%SystemDrive%\oracle\product\19.0.0\dbhome_1
setx ORACLE_HOME %SystemDrive%\oracle\product\19.0.0\dbhome_1 /m
set PERL5LIB=%ORACLE_HOME%/rdbms/admin;%PERL5LIB%
set PATH=%ORACLE_HOME%\bin;%ORACLE_HOME%\perl\bin;%PATH%
copy /y "C:\scripts\init.ora" "%SystemDrive%\oracle\product\19.0.0\dbhome_1\database"
"%SystemDrive%\oracle\product\19.0.0\dbhome_1\bin\oradim.exe" -new -sid %databaseInstanceName% -startmode auto -srvcstart system
start /wait cmd /c "%SystemDrive%\oracle\product\19.0.0\dbhome_1\bin\sqlplus.exe /nolog @C:\scripts\Install.sql %databaseInstanceSYSDBAPassword%"
echo.
echo Setting %databaseInstanceName% database instance as default ORACLE_SID in Windows Registry...
reg add "HKEY_LOCAL_MACHINE\SOFTWARE\Oracle\KEY_OraDB19Home1" /v "ORACLE_SID" /d "%databaseInstanceName%" /f
echo.
echo Generating Oracle Database network configuration files...
set databaseNetConfigFileExists=false
if exist "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora" (
	set databaseNetConfigFileExists=true
	rename "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora" "listener-%date:~0,2%.%date:~3,2%.%date:~6,4%-%time:~0,2%.%time:~3,2%.%time:~6,2%.bak"
	echo Warning! listener.ora file already exists. The file has been backed up and overwritten.
)
@echo # listener.ora Network Configuration File: %SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo # Generated by Oracle configuration tools.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo %databaseListenerName% =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo   (DESCRIPTION =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo     (ADDRESS = (PROTOCOL = TCP)(HOST = 0.0.0.0)(PORT = %databaseListenerPort%))>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo   )>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
@echo.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\listener.ora"
if exist "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora" (
	set databaseNetConfigFileExists=true
	rename "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora" "tnsnames-%date:~0,2%.%date:~3,2%.%date:~6,4%-%time:~0,2%.%time:~3,2%.%time:~6,2%.bak"
	echo Warning! tnsnames.ora file already exists. The file has been backed up and overwritten.
)
@echo # tnsnames.ora Network Configuration File: %SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo # Generated by Oracle configuration tools.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo %databaseInstanceName% =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo   (DESCRIPTION =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo     (ADDRESS_LIST =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo       (ADDRESS = (PROTOCOL = TCP)(HOST = localhost)(PORT = %databaseListenerPort%))>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo     )>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo     (CONNECT_DATA =>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo       (SERVER = DEDICATED)>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo       (SERVICE_NAME = %databaseInstanceName%)>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo     )>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo   )>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo.>> "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\tnsnames.ora"
@echo ALTER SYSTEM SET LOCAL_LISTENER="(ADDRESS = (PROTOCOL = TCP)(HOST = localhost)(PORT = %databaseListenerPort%))";> "C:\scripts\Listener.sql"
powershell -Command "& {((Get-Content -Path "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\sqlnet.ora" -Raw) -replace 'NTS','NONE' | Set-Content -Path "%SystemDrive%\oracle\product\19.0.0\dbhome_1\NETWORK\ADMIN\sqlnet.ora") }"
echo.
if "%databaseNetConfigFileExists%"=="true" (
	echo Existing Oracle Database network configuration files have been updated, please check.
	explorer "%SystemDrive%\oracle\product\19.0.0\dbhome_1\network\admin"
	pause
)
echo.
echo Configuring %databaseListenerName% listener on port %databaseListenerPort%...
lsnrctl start %databaseListenerName%
echo.
echo Binding %databaseInstanceName% database instance to %databaseListenerName% listener...
set oracle_sid=%databaseInstanceName%
echo exit | sqlplus sys/%databaseInstanceSYSDBAPassword% as sysdba @"C:\scripts\Listener.sql"
REM Optional policy [pswd never expired]
REM echo exit | sqlplus sys/%databaseInstanceSYSDBAPassword% as sysdba @"C:\scripts\db\password_policy.sql"
echo Configuring services...
sc config "OracleJobScheduler%databaseInstanceName%" start=auto
sc config "OracleOraDB19Home1TNSListener" start=auto